// --- [ Memory expressions ] --------------------------------------------------

// https://llvm.org/docs/LangRef.html#constant-expressions

// ~~~ [ getelementptr ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ref: ParseValID

GetElementPtrExpr
	: "getelementptr" OptInBounds "(" Type "," Type Constant "," GEPConstIndices ")"
	<< &ast.GetElementPtrExpr{InBounds: $1.(bool), ElemType: $3.(ast.Type), Src: astx.TypeConst($5, $6), Indices: $8.([]*ast.GEPConstIndex)}, nil >>
;

// ref: ParseGlobalValueVector
//
//   ::= empty
//   ::= [inrange] TypeAndValue (',' [inrange] TypeAndValue)*

GEPConstIndices
	: empty               << ([]*ast.GEPConstIndex)(nil), nil >>
	| GEPConstIndexList
;

GEPConstIndexList
	: GEPConstIndex                         << astx.NewGEPConstIndexList($0) >>
	| GEPConstIndexList "," GEPConstIndex   << astx.AppendGEPConstIndex($0, $2) >>
;

GEPConstIndex
	: OptInrange Type Constant   << &ast.GEPConstIndex{InRange: $0.(bool), Index: astx.TypeConst($1, $2)}, nil >>
;

OptInrange
	: empty       << false, nil >>
	| "inrange"   << true, nil >>
;

