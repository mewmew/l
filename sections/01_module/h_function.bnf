// --- [ Function Declaration ] ------------------------------------------------

// https://llvm.org/docs/LangRef.html#functions

/*
/// toplevelentity
///   ::= 'declare' FunctionHeader
bool LLParser::ParseDeclare() {
*/

// NOTE: MetadataAttachment* not part of BNF comment; it should be.

FunctionDecl
	: "declare" OptExternLinkage FunctionHeader
	//: "declare" MetadataAttachments OptExternLinkage FunctionHeader // TODO: Add MetadataAttachments
;

// --- [ Function Definition ] -------------------------------------------------

// https://llvm.org/docs/LangRef.html#functions

/*
/// toplevelentity
///   ::= 'define' FunctionHeader (!dbg !56)* '{' ...
bool LLParser::ParseDefine() {
*/

FunctionDef
	: "define" OptLinkage FunctionHeader FunctionBody
	//: "define" OptLinkage FunctionHeader OptFunctionMetadata FunctionBody // TODO: Add OptFunctionMetadata
;

/*
/// FunctionHeader
///   ::= OptionalLinkage OptionalPreemptionSpecifier OptionalVisibility
///       OptionalCallingConv OptRetAttrs OptUnnamedAddr Type GlobalName
///       '(' ArgList ')' OptFuncAttrs OptSection OptionalAlign OptGC
///       OptionalPrefix OptionalPrologue OptPersonalityFn
bool LLParser::ParseFunctionHeader(Function *&Fn, bool isDefine) {
*/

// NOTE: OptAlignment has been removed to resolve an LR-1 conflict. Alignment
// can still be specified with FnAttributeValuePairs.

// TODO: Rename FnAttributeValuePairs to FuncAttrs.

FunctionHeader
	: OptPreemptionSpecifier OptVisibility OptDLLStorageClass OptCallingConv ReturnAttrs Type GlobalIdent "(" Args ")" OptUnnamedAddr FnAttributeValuePairs OptSection OptComdat OptGC OptPrefix OptPrologue OptPersonality
;

/*
/// ParseOptionalCallingConv
///   ::= empty
///   ::= 'ccc'
///   ::= 'fastcc'
///   ::= 'intel_ocl_bicc'
///   ::= 'coldcc'
///   ::= 'x86_stdcallcc'
///   ::= 'x86_fastcallcc'
///   ::= 'x86_thiscallcc'
///   ::= 'x86_vectorcallcc'
///   ::= 'arm_apcscc'
///   ::= 'arm_aapcscc'
///   ::= 'arm_aapcs_vfpcc'
///   ::= 'msp430_intrcc'
///   ::= 'avr_intrcc'
///   ::= 'avr_signalcc'
///   ::= 'ptx_kernel'
///   ::= 'ptx_device'
///   ::= 'spir_func'
///   ::= 'spir_kernel'
///   ::= 'x86_64_sysvcc'
///   ::= 'win64cc'
///   ::= 'webkit_jscc'
///   ::= 'anyregcc'
///   ::= 'preserve_mostcc'
///   ::= 'preserve_allcc'
///   ::= 'ghccc'
///   ::= 'swiftcc'
///   ::= 'x86_intrcc'
///   ::= 'hhvmcc'
///   ::= 'hhvm_ccc'
///   ::= 'cxx_fast_tlscc'
///   ::= 'amdgpu_vs'
///   ::= 'amdgpu_ls'
///   ::= 'amdgpu_hs'
///   ::= 'amdgpu_es'
///   ::= 'amdgpu_gs'
///   ::= 'amdgpu_ps'
///   ::= 'amdgpu_cs'
///   ::= 'amdgpu_kernel'
///   ::= 'cc' UINT
///
bool LLParser::ParseOptionalCallingConv(unsigned &CC) {
*/

OptCallingConv
	: empty
	| CallingConv
;

CallingConv
	: "ccc"
	| "fastcc"
	| "coldcc"
	| "x86_stdcallcc"
	| "x86_fastcallcc"
	| "x86_regcallcc"
	| "x86_thiscallcc"
	| "x86_vectorcallcc"
	| "arm_apcscc"
	| "arm_aapcscc"
	| "arm_aapcs_vfpcc"
	| "msp430_intrcc"
	| "avr_intrcc"
	| "avr_signalcc"
	| "ptx_kernel"
	| "ptx_device"
	| "spir_kernel"
	| "spir_func"
	| "intel_ocl_bicc"
	| "x86_64_sysvcc"
	| "win64cc"
	| "webkit_jscc"
	| "anyregcc"
	| "preserve_mostcc"
	| "preserve_allcc"
	| "ghccc"
	| "swiftcc"
	| "x86_intrcc"
	| "hhvmcc"
	| "hhvm_ccc"
	| "cxx_fast_tlscc"
	| "amdgpu_vs"
	| "amdgpu_ls"
	| "amdgpu_hs"
	| "amdgpu_es"
	| "amdgpu_gs"
	| "amdgpu_ps"
	| "amdgpu_cs"
	| "amdgpu_kernel"
	| "cc" int_lit
;

OptGC
	: empty
	| "gc" string_lit
;

OptPrefix
	: empty
	| "prefix" Type Constant
;

OptPrologue
	: empty
	| "prologue" Type Constant
;

OptPersonality
	: empty
	| "personality" Type Constant
;

/*
/// ParseOptionalReturnAttrs - Parse a potentially empty list of return attributes.
bool LLParser::ParseOptionalReturnAttrs(AttrBuilder &B) {
*/

ReturnAttrs
	: empty
	| ReturnAttrList
;

ReturnAttrList
	: ReturnAttr
	| ReturnAttrList ReturnAttr
;

ReturnAttr
	: string_lit
	| Dereferenceable
	| Alignment
	| "inreg"
	| "noalias"
	| "nonnull"
	| "signext"
	| "zeroext"
;

/*
/// ParseFunctionBody
///   ::= '{' BasicBlock+ UseListOrderDirective* '}'
bool LLParser::ParseFunctionBody(Function &Fn) {
*/
FunctionBody
	// TODO: Handle UseListOrderDirective.
	: "{" BasicBlockList "}"
;

