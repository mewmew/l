// ~~~ [ Named Metadata ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// https://llvm.org/docs/LangRef.html#named-metadata

// ref: ParseNamedMetadata
//
//   !foo = !{ !1, !2 }

NamedMetadataDef
	: MetadataName "=" "!" "{" MetadataNodes "}"
	<< &ast.NamedMetadataDef{Name: $0.(ast.MetadataName), Nodes: $4.([]ast.MetadataNode)}, nil >>
;

MetadataNodes
	: empty
	<< ([]ast.MetadataNode)(nil), nil >>
	| MetadataNodeList
;

MetadataNodeList
	: MetadataNode
	<< []ast.MetadataNode{$0.(ast.MetadataNode)}, nil >>
	| MetadataNodeList "," MetadataNode
	<< append($0.([]ast.MetadataNode), $2.(ast.MetadataNode)), nil >>
;

MetadataNode
	: MetadataID
	// Parse DIExpressions inline as a special case. They are still MDNodes, so
	// they can still appear in named metadata. Remove this logic if they become
	// plain Metadata.
	| DIExpression
;

// ~~~ [ Standalone Metadata ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// https://llvm.org/docs/LangRef.html#metadata-nodes-and-metadata-strings

// ref: ParseStandaloneMetadata
//
//   !42 = !{...}

MetadataDef
	: MetadataID "=" OptDistinct MDTuple
	<< &ast.MetadataDef{Name: $0.(ast.MetadataID), Distinct: $2.(bool), Node: $3.(ast.MDNode)}, nil >>
	| MetadataID "=" OptDistinct SpecializedMDNode
	<< &ast.MetadataDef{Name: $0.(ast.MetadataID), Distinct: $2.(bool), Node: $3.(ast.MDNode)}, nil >>
;

OptDistinct
	: empty        << false, nil >>
	| "distinct"   << true, nil >>
;

