// ~~~ [ Named Metadata ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// https://llvm.org/docs/LangRef.html#named-metadata

// ref: ParseNamedMetadata
//
//   !foo = !{ !1, !2 }

NamedMetadataDef
	: MetadataName "=" "!" "{" MetadataNodes "}"   << astx.NewNamedMetadataDef($0, $4) >>
;

MetadataNodes
	: empty
	| MetadataNodeList
;

MetadataNodeList
	: MetadataNode                        << astx.NewMetadataNodeList($0) >>
	| MetadataNodeList "," MetadataNode   << astx.AppendMetadataNode($0, $2) >>
;

MetadataNode
	: MetadataID
	// Parse DIExpressions inline as a special case. They are still MDNodes, so
	// they can still appear in named metadata. Remove this logic if they become
	// plain Metadata.
	| DIExpression
;

// ~~~ [ Standalone Metadata ] ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// https://llvm.org/docs/LangRef.html#metadata-nodes-and-metadata-strings

// ref: ParseStandaloneMetadata
//
//   !42 = !{...}

MetadataDef
	: MetadataID "=" OptDistinct MDTuple             << astx.NewMetadataDef($0, $2, $3) >>
	| MetadataID "=" OptDistinct SpecializedMDNode   << astx.NewMetadataDef($0, $2, $3) >>
;

OptDistinct
	: empty        << false, nil >>
	| "distinct"   << true, nil >>
;

