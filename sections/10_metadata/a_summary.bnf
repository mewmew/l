// === [ Metadata Nodes and Metadata Strings ] =================================

// https://llvm.org/docs/LangRef.html#metadata-nodes-and-metadata-strings

// --- [ Metadata Tuple ] ------------------------------------------------------

// ref: ParseMDTuple

MDTuple
	: "!" MDFields
	<< $1, nil >>
;

// ref: ParseMDNodeVector
//
//   ::= { Element (',' Element)* }
//  Element
//   ::= 'null' | TypeAndValue

// ref: ParseMDField(MDFieldList &)

MDFields
	: "{" "}"
	| "{" MDFieldList "}"
;

MDFieldList
	: MDField
	| MDFieldList "," MDField
;

// ref: ParseMDField(MDField &)

MDField
	// Null is a special case since it is typeless.
	: NullConst
	| Metadata
;

// --- [ Metadata ] ------------------------------------------------------------

// ref: ParseMetadata
//
//  ::= i32 %local
//  ::= i32 @global
//  ::= i32 7
//  ::= !42
//  ::= !{...}
//  ::= !"string"
//  ::= !DILocation(...)

Metadata
	: Type Value
	<< &ast.MDTypeValue{Type: $0.(ast.Type), Value: $1.(ast.Value)}, nil >>
	| MDString
	// !{ ... }
	| MDTuple
	// !7
	| MetadataID
	| SpecializedMDNode
;

// --- [ Metadata String ] -----------------------------------------------------

// ref: ParseMDString
//
//   ::= '!' STRINGCONSTANT

MDString
	: "!" string_lit
	<< &ast.MDString{Value: astx.String($1)}, nil >>
;

// --- [ Metadata Attachment ] -------------------------------------------------

// ref: ParseMetadataAttachment
//
//   ::= !dbg !42

MetadataAttachment
	: MetadataName MDNode
	<< &ast.MetadataAttachment{Name: $0.(*ast.MetadataName), Node: $1.(ast.MDNode)}, nil >>
;

// --- [ Metadata Node ] -------------------------------------------------------

// ref: ParseMDNode
//
//  ::= !{ ... }
//  ::= !7
//  ::= !DILocation(...)

MDNode
	// !{ ... }
	: MDTuple
	// !42
	| MetadataID
	| SpecializedMDNode
;

// ### [ Helper productions ] ##################################################

// ref: ParseOptionalFunctionMetadata
//
//   ::= (!dbg !57)*

MetadataAttachments
	: empty
	<< ([]*ast.MetadataAttachment)(nil), nil >>
	| MetadataAttachmentList
;

MetadataAttachmentList
	: MetadataAttachment
	<< []*ast.MetadataAttachment{$0.(*ast.MetadataAttachment)}, nil >>
	| MetadataAttachmentList MetadataAttachment
	<< append($0.([]*ast.MetadataAttachment), $1.(*ast.MetadataAttachment)), nil >>
;

// ref: ParseInstructionMetadata
//
//   ::= !dbg !42 (',' !dbg !57)*

OptCommaSepMetadataAttachmentList
	: empty
	<< ([]*ast.MetadataAttachment)(nil), nil >>
	| "," CommaSepMetadataAttachmentList
	<< $1, nil >>
;

CommaSepMetadataAttachmentList
	: MetadataAttachment
	<< []*ast.MetadataAttachment{$0.(*ast.MetadataAttachment)}, nil >>
	| CommaSepMetadataAttachmentList "," MetadataAttachment
	<< append($0.([]*ast.MetadataAttachment), $2.(*ast.MetadataAttachment)), nil >>
;

