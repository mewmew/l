// === [ Terminators ] =========================================================

// https://llvm.org/docs/LangRef.html#terminator-instructions

// ref: ParseInstruction

Terminator
	: RetTerm
	| BrTerm
	| CondBrTerm
	| SwitchTerm
	| IndirectBrTerm
	| InvokeTerm
	| ResumeTerm
	| CatchSwitchTerm
	| CatchRetTerm
	| CleanupRetTerm
	| UnreachableTerm
;

// --- [ ret ] -----------------------------------------------------------------

// https://llvm.org/docs/LangRef.html#ret-instruction

// ref: ParseRet
//
//   ::= 'ret' void (',' !dbg, !1)*
//   ::= 'ret' TypeAndValue (',' !dbg, !1)*

RetTerm
	// Void return.
	: "ret" VoidType OptCommaSepMetadataAttachmentList
	<< &ast.RetTerm{Metadata: $2.([]*ast.MetadataAttachment)}, nil >>
	// Value return.
	| "ret" ConcreteType Value OptCommaSepMetadataAttachmentList
	<< &ast.RetTerm{X: astx.TypeValue($1, $2), Metadata: $3.([]*ast.MetadataAttachment)}, nil >>
;

// --- [ br ] ------------------------------------------------------------------

// https://llvm.org/docs/LangRef.html#br-instruction

// ref: ParseBr
//
//   ::= 'br' TypeAndValue
//   ::= 'br' TypeAndValue ',' TypeAndValue ',' TypeAndValue

// Unconditional branch.
BrTerm
	: "br" LabelType LocalIdent OptCommaSepMetadataAttachmentList
	<< &ast.BrTerm{Target: astx.Label($1, $2), Metadata: $3.([]*ast.MetadataAttachment)}, nil >>
;

// Conditional branch.
CondBrTerm
	: "br" IntType Value "," LabelType LocalIdent "," LabelType LocalIdent OptCommaSepMetadataAttachmentList
	<< &ast.CondBrTerm{Cond: astx.TypeValue($1, $2), TargetTrue: astx.Label($4, $5), TargetFalse: astx.Label($7, $8), Metadata: $9.([]*ast.MetadataAttachment)}, nil >>
;

// --- [ switch ] --------------------------------------------------------------

// https://llvm.org/docs/LangRef.html#switch-instruction

// ref: ParseSwitch
//
//    ::= 'switch' TypeAndValue ',' TypeAndValue '[' JumpTable ']'
//  JumpTable
//    ::= (TypeAndValue ',' TypeAndValue)*

SwitchTerm
	: "switch" Type Value "," LabelType LocalIdent "[" Cases "]" OptCommaSepMetadataAttachmentList
	<< &ast.SwitchTerm{X: astx.TypeValue($1, $2), Default: astx.Label($4, $5), Cases: $7.([]*ast.Case), Metadata: $9.([]*ast.MetadataAttachment)}, nil >>
;

Cases
	: empty
	<< ([]*ast.Case)(nil), nil >>
	| CaseList
;

CaseList
	: Case
	<< []*ast.Case{$0.(*ast.Case)}, nil >>
	| CaseList Case
	<< append($0.([]*ast.Case), $1.(*ast.Case)), nil >>
;

Case
	: Type IntConst "," LabelType LocalIdent
	<< &ast.Case{X: astx.TypeConst($0, $1), Target: astx.Label($3, $4)}, nil >>
;

// --- [ indirectbr ] ----------------------------------------------------------

// https://llvm.org/docs/LangRef.html#indirectbr-instruction

// ref: ParseIndirectBr
//
//    ::= 'indirectbr' TypeAndValue ',' '[' LabelList ']'

IndirectBrTerm
	: "indirectbr" Type Value "," "[" LabelList "]" OptCommaSepMetadataAttachmentList
	<< &ast.IndirectBrTerm{Addr: astx.TypeValue($1, $2), Targets: $5.([]*ast.Label), Metadata: $7.([]*ast.MetadataAttachment)}, nil >>
;

LabelList
	: Label
	<< []*ast.Label{$0.(*ast.Label)}, nil >>
	| LabelList "," Label
	<< append($0.([]*ast.Label), $2.(*ast.Label)), nil >>
;

Label
	: LabelType LocalIdent
	<< &ast.Label{Name: $1.(*ast.LocalIdent)}, nil >>
;

// --- [ invoke ] --------------------------------------------------------------

// https://llvm.org/docs/LangRef.html#invoke-instruction

// ref: ParseInvoke
//
//   ::= 'invoke' OptionalCallingConv OptionalAttrs Type Value ParamList
//       OptionalAttrs 'to' TypeAndValue 'unwind' TypeAndValue

InvokeTerm
	: "invoke" OptCallingConv ReturnAttrs Type Value "(" Args ")" FuncAttrs OperandBundles "to" LabelType LocalIdent "unwind" LabelType LocalIdent OptCommaSepMetadataAttachmentList
	<< &ast.InvokeTerm{CallingConv: $1.(ast.CallingConv), ReturnAttrs: $2.([]ast.ReturnAttribute), Callee: astx.TypeValue($3, $4), Args: $6.([]ast.Argument), FuncAttrs: $8.([]ast.FuncAttribute), OperandBundles: $9.([]*ast.OperandBundle), Normal: astx.Label($11, $12), Exception: astx.Label($14, $15), Metadata: $16.([]*ast.MetadataAttachment)}, nil >>
;

// --- [ resume ] --------------------------------------------------------------

// https://llvm.org/docs/LangRef.html#resume-instruction

// ref: ParseResume
//
//   ::= 'resume' TypeAndValue

ResumeTerm
	: "resume" Type Value OptCommaSepMetadataAttachmentList
	<< &ast.ResumeTerm{X: astx.TypeValue($1, $2), Metadata: $3.([]*ast.MetadataAttachment)}, nil >>
;

// --- [ catchswitch ] ---------------------------------------------------------

// https://llvm.org/docs/LangRef.html#catchswitch-instruction

// ref: ParseCatchSwitch
//
//   ::= 'catchswitch' within Parent

CatchSwitchTerm
	: "catchswitch" "within" ExceptionScope "[" LabelList "]" "unwind" UnwindTarget OptCommaSepMetadataAttachmentList
	<< &ast.CatchSwitchTerm{Scope: $2.(ast.ExceptionScope), Handlers: $4.([]*ast.Label), UnwindTarget: $7.(ast.UnwindTarget), Metadata: $8.([]*ast.MetadataAttachment)}, nil >>
;

UnwindTarget
	: "to" "caller"
	<< &ast.UnwindToCaller{}, nil >>
	| LabelType LocalIdent
	<< astx.Label($0, $1), nil >>
;

// --- [ catchret ] ------------------------------------------------------------

// https://llvm.org/docs/LangRef.html#catchret-instruction

// ref: ParseCatchRet
//
//   ::= 'catchret' from Parent Value 'to' TypeAndValue

CatchRetTerm
	: "catchret" "from" Value "to" LabelType LocalIdent OptCommaSepMetadataAttachmentList
	<< &ast.CatchRetTerm{From: $2.(ast.Value), To: astx.Label($4, $5), Metadata: $6.([]*ast.MetadataAttachment)}, nil >>
;

// --- [ cleanupret ] ----------------------------------------------------------

// https://llvm.org/docs/LangRef.html#cleanupret-instruction

// ref: ParseCleanupRet
//
//   ::= 'cleanupret' from Value unwind ('to' 'caller' | TypeAndValue)

CleanupRetTerm
	: "cleanupret" "from" Value "unwind" UnwindTarget OptCommaSepMetadataAttachmentList
	<< &ast.CleanupRetTerm{From: $2.(ast.Value), UnwindTarget: $4.(ast.UnwindTarget), Metadata: $5.([]*ast.MetadataAttachment)}, nil >>
;

// --- [ unreachable ] ---------------------------------------------------------

// https://llvm.org/docs/LangRef.html#unreachable-instruction

// ref: ParseInstruction

UnreachableTerm
	: "unreachable" OptCommaSepMetadataAttachmentList
	<< &ast.UnreachableTerm{Metadata: $1.([]*ast.MetadataAttachment)}, nil >>
;

